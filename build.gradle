plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'com.github.gmazzo.buildconfig' version '3.0.1'
    id 'java'
}

group = bot_group
version = bot_version

buildConfig {
    className('EzriqueInfo')
    packageName('xyz.deftu.ezrique')

    buildConfigField('String', 'VERSION', "\"${bot_version}\"")
}

repositories {
    mavenCentral()

    maven {
        name = 'Qalcyo'
        url = 'http://maven.deftu.xyz/repository/maven-public/'
        allowInsecureProtocol = true
    }
    maven {
        name = 'Dv8tion'
        url = 'https://m2.dv8tion.net/releases/'
    }
    maven {
        name = 'JitPack'
        url = 'https://jitpack.io/'
    }
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

dependencies {
    shade('xyz.qalcyo.simpleconfig:SimpleConfig:3.5')
    shade('xyz.qalcyo.simpleconfig:SimpleConfig:3.5:settings')
    shade('xyz.qalcyo.json:JSON+:2.12')
    shade('xyz.qalcyo.json:JSON+:2.12:files')
    shade('xyz.qalcyo.json:JSON+:2.12:kotlin')
    shade('xyz.qalcyo.json:JSON+:2.12:serialization')
    shade('xyz.qalcyo.json:JSON+:2.12:utilities')
    shade('xyz.qalcyo.mango:Mango:1.8')

    shade('org.apache.logging.log4j:log4j-api:2.14.1')
    shade('org.apache.logging.log4j:log4j-core:2.14.1')
    shade('org.apache.logging.log4j:log4j-slf4j18-impl:2.14.1')

    shade('net.dv8tion:JDA:4.3.0_346')
    shade('org.mongodb:mongodb-driver-sync:4.3.0')
}

jar {
    manifest.attributes(
            'Main-Class': 'xyz.deftu.ezrique.Bootstrap'
    )
}

shadowJar {
    configurations = [project.configurations.shade]
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveClassifier.set('')
}

jar.finalizedBy(shadowJar)